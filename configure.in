# $Id: configure.in,v 1.33 2004-07-14 09:26:22 yosimoto Exp $

AC_PREREQ([2.54])

m4_define([debug_default], [no])
m4_define([libcam1394_version], [0.1.9rc6])

AC_INIT([libcam1394], [libcam1394_version], [yosimoto@limu.is.kyushu-u.ac.jp])
AM_INIT_AUTOMAKE([1.7 no-define])
AM_CONFIG_HEADER(config.h)

# Save this value here, since automake will set cflags later
cflags_set=${CFLAGS+set}
cxxflags_set=${CXXFLAGS+set}

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL

AC_PATH_XTRA 
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h fcntl.h netinet/in.h string.h sys/ioctl.h unistd.h])
AC_CHECK_HEADERS(cv.h)
AC_CHECK_HEADERS(opencv/cv.h)

AC_CHECK_LIB(popt,poptGetContext)
AC_LIB_RAW1394(0.9)
if test "$ac_libraw1394_found" != "yes"; then
	AC_MSG_ERROR( not found libraw1394(>=0.9) )
fi

# Checks for typedefs, structures, and compiler characteristics.
AC_LANG_CPLUSPLUS
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

# Checks for library functions.
#AC_PROG_GCC_TRADITIONAL
#AC_FUNC_MMAP
AC_CHECK_FUNCS([memset strcasecmp strerror strtol])

AC_ARG_WITH([isofb],
	AC_HELP_STRING([--with-isofb],
	[use isofb driver @<:@default=no@:>@]),
	 driver="isofb"
         CFLAGS="-DHAVE_ISOFB $CFLAGS"
         CXXFLAGS="-DHAVE_ISOFB $CXXFLAGS",
	driver="video1394")


AC_ARG_ENABLE([debug],
        AC_HELP_STRING([--enable-debug],
                [turn on debugging @<:@default=debug_default@:>@]), ,
        [enable_debug=debug_default])


if test "x$enable_debug" = "xyes"; then
  if test x$cflags_set != xset ; then
      CFLAGS="-DDEBUG -g $CFLAGS"
      CXXFLAGS="-DDEBUG -g $CXXFLAGS"
  fi
fi


lt_major=1
lt_revision=0
lt_age=0
AC_SUBST([lt_major])
AC_SUBST([lt_revision])
AC_SUBST([lt_age])
CFLAGS="$CFLAGS -Wall -Wunused"

AC_OUTPUT([
Makefile
libcam1394/Makefile
libcam1394/Doxyfile
cam1394/Makefile
sample/Makefile.opencv
sample/Makefile
])



##################################################
# Print configuration
##################################################
if test "x$ac_cv_header_opencv_cv_h" = "xyes"; then
  is_opencv_supported="yes"
elif test "x$ac_cv_header_cv_h" = "xyes"; then
  is_opencv_supported="yes"
else
  is_opencv_supported="no"
fi

echo "
configuration:	
        debug:   $enable_debug
       driver:   $driver
opecv support:   $is_opencv_supported
"
